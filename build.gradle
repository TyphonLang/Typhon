apply plugin: 'java'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'antlr'
apply plugin: 'jacoco'
apply plugin: 'maven'

// variables

group = 'info.iconmaster'
version = '0.2.3'

def mainClass = 'info.iconmaster.typhon.Typhon'
def processedSrcDir = "${buildDir}/generated-src/java/main"

// define sources

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.12'
	antlr "org.antlr:antlr4:4.5"
}

buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.6.1'
		classpath 'org.apache.maven.wagon:wagon-http:2.2'
	}
}

// define Launch4J settings

launch4j {
	mainClassName = mainClass
	headerType = "console"
}

// define ANTLR settings

generateGrammarSource {
    arguments += ["-visitor", "-package", "info.iconmaster.typhon.antlr"]
}

// add code coverage support

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

// add Bintray support

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "https://api.bintray.com/maven/iconmaster5326/maven/typhon/;publish=1") {
            authentication(userName: project.hasProperty('bintrayUser') ? project.property('bintrayUser') : '', password: project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : '')
        }
    }
}

// define artifacts - the .class and .java JARS

jar {
    manifest {
        attributes 'Main-Class': mainClass
    }
    
    // uncomment the next line if you want a standalone jar
    // from configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Main-Class': mainClass
    }
    
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
	archives jar
    archives sourcesJar
}